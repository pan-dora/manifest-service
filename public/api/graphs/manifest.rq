PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sc: <http://iiif.io/api/presentation/2#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX exif: <http://www.w3.org/2003/12/exif/ns#>
PREFIX svcs: <http://rdfs.org/sioc/services#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fedora: <http://fedora.info/definitions/v4/repository#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX doap: <http://usefulinc.com/ns/doap#>
CONSTRUCT {?manifest rdf:type sc:Manifest .
?manifest sc:hasSequences ?seqid .
?manifest rdfs:label ?label .
?seqid rdf:first ?sequence .
?seqid rdf:rest ?seqrest .
?sequence rdf:type sc:Sequence .
?sequence sc:hasCanvases ?canvaslist .
?sequence sc:viewingHint sc:pagedHint .
?mid rdf:first ?canvas .
?mid rdf:rest ?last .
?canvas rdf:type sc:Canvas .
?canvas exif:height ?height  .
?canvas exif:width ?width  .
?canvas rdfs:label ?canvaslabel .
?canvas sc:hasImageAnnotations ?imageid .
?imageid rdf:first ?resid .
?imageid rdf:rest rdf:nil .
?resid rdf:type oa:Annotation.
?resid oa:hasBody ?image .
?resid oa:hasTarget ?canvas .
?resid oa:motivatedBy sc:painting .
?image svcs:has_service ?service .
?service rdf:type <http://iiif.io/api/image/2/context.json> .
?service doap:implements <http://iiif.io/api/image/2/level1.json> .
?image rdf:type dctypes:Image .
?image exif:height ?iheight.
?image exif:width ?iwidth .
?image dc:format ?format .
?canvas sc:hasLists ?listid .
?mid2 rdf:first ?annolistid .
?annolistid svcs:has_service ?listservice .
?mid2 rdf:rest ?annolistidrest .
?annolistid rdf:type sc:AnnotationList .
?manifest sc:hasRanges ?rangelist .
?rangelist rdf:first ?range .
?range rdf:type sc:Range .
?rangelist rdf:rest ?rangerest .
?range sc:hasParts ?parts .
?manifest svcs:has_service ?searchservice .
?searchservice doap:implements <http://iiif.io/api/search/0/search>
}

WHERE {values ?manifest {<http://localhost:8080/fcrepo/rest/collection/?node/manifest>} .
values ?searchservice {<http://localhost:3000/api/graphs/listsearch.nt?$q=>} .
?manifest sc:hasSequences ?seqid .
OPTIONAL {?manifest rdfs:label ?label } .
?seqid rdf:first ?sequence .
?seqid rdf:rest ?seqrest .
?sequence sc:hasCanvases ?canvaslist .
?canvaslist rdf:rest* ?mid .
?mid rdf:first ?canvas .
?mid rdf:rest ?last .
?canvas sc:hasImageAnnotations ?imageid .
?canvas exif:height ?height .
?canvas exif:width ?width .
OPTIONAL {?canvas rdfs:label ?canvaslabel }.
?imageid rdf:first ?resid .
?resid oa:hasBody ?image .
?image svcs:has_service ?service .
?image exif:height ?iheight .
?image exif:width ?iwidth .
?image dc:format ?format .
?canvas sc:hasLists ?listid .
?listid rdf:rest* ?mid2 .
?mid2 rdf:first ?annolistid .
OPTIONAL {?annolistid svcs:has_service ?listservice } .
?mid2 rdf:rest ?annolistidrest .
OPTIONAL {
?manifest sc:hasRanges ?rangelist .
?rangelist rdf:first ?range .
?rangelist rdf:rest ?rangerest .
?range sc:hasParts ?parts }
}

